plugins {
    id 'org.liquibase.gradle' version '2.0.4'
    id 'org.springframework.boot' version "${springBootVersion}"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'base'
apply plugin: 'java'

dependencies {
    liquibaseRuntime 'org.liquibase:liquibase-core'
    liquibaseRuntime 'org.postgresql:postgresql'
    liquibaseRuntime 'ch.qos.logback:logback-core'
    liquibaseRuntime 'ch.qos.logback:logback-classic'
    liquibaseRuntime 'org.yaml:snakeyaml'

    compile('org.testcontainers:jdbc:1.15.1')

    testImplementation 'junit:junit'
    testImplementation configurations.liquibaseRuntime
}

bootJar {
    enabled = false
}

sourceSets {
    test {
        resources {
            srcDir('liquibase')
        }
    }
}

liquibase {
    activities {
        main {
            changeLogFile 'liquibase/db/changelog/db.changelog-master.yaml'
            url "jdbc:postgresql://$host:$port/$database"
            driver 'org.postgresql.Driver'
            username "$user"
            password "$pass"
            contexts "$contexts"
        }
    }
}

task liquibasePackage {
    group = 'liquibase'
    description = 'Packages changelog and jars required to run liquibase'

    doLast {
        copy {
            from 'liquibase'
            into "${buildDir}/liquibase"
        }
        copy {
            from configurations.liquibaseRuntime
            into "${buildDir}/liquibase/libs"
        }
    }
}
assemble.dependsOn liquibasePackage